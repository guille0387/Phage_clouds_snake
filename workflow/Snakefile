configfile: "config.yaml"

rule all:
    input:
        f'{config["host"]}_{config["threshold"]}_clouds.html'

rule get_fasta:
    input:
        config["input_dir"]
    output:
        "all_gbk_records.fna"
    conda:
        "envs/phage_clouds.yaml"
    script:
        "scripts/get_fasta_from_gbk.py"

rule ref_sketch:
    input:
        "all_gbk_records.fna"
    output:
        "all_gbk_records_k15_s24000.msh"
    threads: 10
    run:
        print('mash sketching parameters: kmer_size = 15, sketch_size = 24000')
        shell("mash sketch -p 10 -i -o {output} -k 15 -s 24000 {input}")

rule mash_dist:
    input:
        "all_gbk_records_k15_s24000.msh",
        "all_gbk_records.fna"
    output:
        "all_vs_all_nucl_k15_s24000.tbl"
    threads: 10
    run:
        print('Calculating all vs all mash distance table (p-value reporting threshold 1e-10)...')
        shell("mash dist -p 10 -i -t -v 1e-10 {input} > {output}")
        print(f'Done creating {output}')

rule annot_dict:
    input:
        config["input_dir"]
    output:
        "gbk_records_metadata.pkl"
    conda:
        "envs/phage_clouds.yaml"
    script:
        "scripts/get_metadata_dict.py"

rule get_network:
    input:
        mashtbl="all_vs_all_nucl_k15_s24000.tbl",
        annotdict="gbk_records_metadata.pkl"
    output:
        plaingraph="all_gbk_records_graph.nxpkl.gz",
        annotgraph="all_gbk_records_graph_annotated.nxpkl.gz"
    conda:
        "envs/phage_clouds.yaml"
    script:
        "scripts/generate_graph.py"

rule draw_graph:
    input:
        "all_gbk_records_graph_annotated.nxpkl.gz"
    output:
        f'{config["host"]}_{config["threshold"]}_clouds.html'
    conda:
        "envs/phage_clouds.yaml"
    params:
        host=config["host"],
        thres=config["threshold"]
    script:
        "scripts/draw_graph.py"
